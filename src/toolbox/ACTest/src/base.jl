#
# Project : Lily
# Source  : base.jl
# Author  : Li Huang (huangli@caep.cn)
# Status  : Unstable
#
# Last modified: 2025/05/07
#

"""
    reprod(am::AbstractMesh, kernel::Matrix{F64}, A::Vector{F64})

Try to reproduce the input data, which can be compared with the raw data
to see whether the analytic continuation is reasonable.

### Arguments
* am -> Real frequency mesh.
* kernel -> The kernel function.
* A -> The calculated spectral function, A(Ï‰).

### Returns
* G -> Reconstructed correlators, G(Ï„) or G(iÏ‰â‚™), Vector{F64}.

See also: [`AbstractMesh`](@ref).
"""
function reprod(am::AbstractMesh, kernel::Matrix{F64}, A::Vector{F64})
    ndim, nmesh = size(kernel)
    @assert nmesh == length(am) == length(A)

    @einsum KA[i,j] := kernel[i,j] * A[j]

    G = zeros(F64, ndim)
    for i = 1:ndim
        G[i] = trapz(am, view(KA, i, :))
    end

    return G
end

"""
    setup_param(C::Dict{String,Any}, reset::Bool = true)

Setup the configuration dictionaries via function call. Here `C` contains
parameters for general setup. If `reset` is true, then the configuration
dictionaries will be reset to their default values at first. Later, `C`
will be used to customized the dictionaries further.

### Arguments
See above explanations.

### Returns
N/A

See also: [`read_param`](@ref).
"""
function setup_param(C::Dict{String,Any}, reset::Bool = true)
    # _PTEST contains the default parameters.
    # If reset is true, it will be used to update the PTEST dictionary.
    reset && rev_dict(_PTEST)
    rev_dict(C)
end

"""
    read_param()

Setup the configuration dictionaries via an external file. The valid
format of a configuration file is `toml`.

### Arguments
N/A

### Returns
N/A

See also: [`setup_param`](@ref).
"""
function read_param()
    cfg = inp_toml(query_args(), true)
    fil_dict(cfg)
    chk_dict()
    see_dict()
end

"""
    make_data_std()

Try to build a standard dataset (`ACT100`), which contains 100 typical
spectral functions and the corresponding correlation functions. These
spectral functions are defined in `src/dataset.jl`. Please use the script
`util/acstd.jl` to do this job.

### Arguments
N/A

### Returns
N/A

See also: [`make_data`](@ref).
"""
function make_data_std()
    # Get dicts for the standard test (ACT100)
    ACT100 = union(STD_FG, STD_FD, STD_FRD, STD_BG, STD_BD, STD_BRD)

    # We have to make sure ntest == 100
    ntest = get_t("ntest")
    @assert ntest == length(ACT100) == 100

    # Initialize the random number generator
    # It is used to generate random noise only.
    seed = rand(1:10000) * myid() + 1981
    rng = MersenneTwister(seed)
    println("Random number seed: ", seed)

    # Start the loop
    println()
    for i = 1:ntest
        @printf("Test -> %6i / %6i\n", i, ntest)

        # Get dict for current test
        dict = ACT100[i]

        # Prepare grid for input data
        grid = make_grid(dict["grid"])
        println("Build grid for input data: ", length(grid), " points")

        # Prepare mesh for output spectrum
        mesh = make_mesh(dict["ktype"], dict["mesh"])
        println("Build mesh for spectrum: ", length(mesh), " points")

        # Prepare kernel function
        kernel = make_kernel(mesh, grid)
        println("Build default kernel: ", dict["ktype"])

        # Generate spectral functions
        sf = make_spectrum(mesh, dict["peaks"], dict["signs"])
        #
        # Generate Green's functions
        green = make_green(rng, sf, kernel, grid)
        #
        # Write generated data
        write_spectrum(i, sf)
        write_backward(i, green)
        #
        println()
    end
end

"""
    make_data()

Try to generate spectral functions and the corresponding Green's functions.

### Arguments
N/A

### Returns
N/A

See also: [`make_data_std`](@ref).
"""
function make_data()
    # Get number of tests
    ntest = get_t("ntest")

    # Initialize the random number generator
    seed = rand(1:10000) * myid() + 1981
    rng = MersenneTwister(seed)
    println("Random number seed: ", seed)

    # Prepare grid for input data
    grid = make_grid()
    println("Build grid for input data: ", length(grid), " points")

    # Prepare mesh for output spectrum
    mesh = make_mesh()
    println("Build mesh for spectrum: ", length(mesh), " points")

    # Prepare kernel function
    kernel = make_kernel(mesh, grid)
    println("Build default kernel: ", get_t("ktype"))

    # Start the loop
    println()
    for i = 1:ntest
        @printf("Test -> %6i / %6i\n", i, ntest)
        #
        # Generate spectral functions
        sf = make_spectrum(rng, mesh)
        #
        # Generate Green's functions
        green = make_green(rng, sf, kernel, grid)
        #
        # Write generated data
        write_spectrum(i, sf)
        write_backward(i, green)
        #
        println()
    end
end

"""
    make_peak(rng::AbstractRNG)

Generate peak to build the final spectral function.

### Arguments
* rng -> Random number generator.

### Returns
* ð‘ -> A Peak struct (subtype of AbstractPeak).

See also: [`AbstractPeak`](@ref).
"""
function make_peak(rng::AbstractRNG)
    # Get essential parameters
    ptype = get_t("ptype")
    pmax  = get_t("pmax")
    pmin  = get_t("pmin")

    # Special treatment for ptype
    if ptype in ["random1", "random2"]
        if ptype == "random1"
            plist = ["gauss", "lorentz", "risedecay"]
        else
            plist = ["gauss", "lorentz", "delta", "rectangle", "risedecay"]
        end
        ptype, = rand(plist, 1)
        println("ptype is randomized to: $ptype")
    end

    @cswitch ptype begin
        @case "gauss"
            A = rand(rng)
            Î“ = rand(rng)
            Ïµ = rand(rng) * (pmax - pmin) + pmin
            ð‘ = GaussianPeak(A, Î“, Ïµ)
            break

        @case "lorentz"
            A = rand(rng)
            Î“ = rand(rng)
            Ïµ = rand(rng) * (pmax - pmin) + pmin
            ð‘ = LorentzianPeak(A, Î“, Ïµ)
            break

        @case "delta"
            A = rand(rng)
            Î“ = 0.02 # Very sharp gaussian peak
            Ïµ = rand(rng) * (pmax - pmin) + pmin
            ð‘ = DeltaPeak(A, Î“, Ïµ)
            break

        @case "rectangle"
            c = rand(rng) * (pmax - pmin) + pmin
            w = rand(rng) * min(c - pmin, pmax - c) * 2.0
            h = rand(rng)
            @assert pmin â‰¤ c - w/2.0 â‰¤ c + w/2.0 â‰¤ pmax
            ð‘ = RectanglePeak(c, w, h)
            break

        @case "risedecay"
            c = rand(rng) * (pmax - pmin) + pmin
            Î³ = rand(rng) * 2.0 # Î³ âˆˆ (0.0,2.0)
            h = rand(rng)
            ð‘ = RiseDecayPeak(c, Î³, h)
            break

        @default
            sorry()
            break
    end
    println(ð‘)

    return ð‘
end

"""
    make_spectrum(rng::AbstractRNG, mesh::AbstractMesh)

Generate a spectral function randomly at given mesh.

### Arguments
* rng -> Random number generator.
* mesh -> Real frequency mesh, Ï‰.

### Returns
* sf -> A SpectralFunction struct.

See also: [`make_data`](@ref).
"""
function make_spectrum(rng::AbstractRNG, mesh::AbstractMesh)
    # Extract essential parameters
    ktype = get_t("ktype")
    offdiag = get_t("offdiag")
    lpeak = get_t("lpeak")

    # Get number of peaks
    npeak, = rand(rng, lpeak, 1)
    @printf("number of peaks : %2i\n", npeak)

    # Determine signs for all peaks
    signs = ones(F64, npeak)
    if offdiag
        # How many negative signs are there?
        # We have to make sure that at least one sign is negative.
        nsign = rand(rng, 1:npeak)
        selected = rand(rng, 1:npeak, nsign)
        signs[selected] .= -1.0
        @assert count(x -> x < 0.0, signs) â‰¥ 1
    end

    image = zeros(F64, length(mesh))
    #
    for sign in signs
        # Get sign for the current peak
        @printf("sign : %4.2f\n", sign)
        #
        # Generate peak
        ð‘ = make_peak(rng)
        #
        # Add up to the spectrum
        image = image + sign * ð‘(mesh)
    end
    #
    # Normalize the spectrum
    if !offdiag
        image = image ./ trapz(mesh,image)
    else
        # We have to make sure that A(Ï‰) should exhibit negative weights
        # at some points.
        #
        # For fermionic systems
        if ktype == "fermi"
            # A(Ï‰) > 0
            if count(x -> x < 0.0, image) == 0
                @. image = image * (-1.0)
            end
            #
            @assert any(x -> x < 0.0, image)
        # For bosonic systems
        else
            # A(Ï‰) > 0
            if count(x -> x < 0.0, image .* mesh.mesh) == 0
                @. image = image * (-1.0)
            end
            #
            @assert any(x -> x < 0.0, image .* mesh.mesh)
        end
    end

    return SpectralFunction(mesh, image)
end

"""
    make_spectrum(
        mesh::AbstractMesh,
        pv::Vector{<:AbstractPeak},
        sv::Vector{F64}
    )

Generate a spectral function with given peaks and signs at given mesh.

### Arguments
* mesh -> Real frequency mesh, Ï‰.
* pv -> List of peaks.
* sv -> List of signs.

### Returns
* sf -> A SpectralFunction struct.

See also: [`make_data_std`](@ref).
"""
function make_spectrum(
    mesh::AbstractMesh,
    pv::Vector{<:AbstractPeak},
    sv::Vector{F64}
    )
    # Get number of peaks
    npeak = length(pv)
    @assert length(pv) == length(sv)
    @printf("number of peaks : %2i\n", npeak)

    image = zeros(F64, length(mesh))
    #
    for i = 1:npeak
        # Determine sign of the current peak
        @printf("sign : %4.2f\n", sv[i])
        #
        # Generate peak
        ð‘ = pv[i]
        println(ð‘)
        #
        # Add up to the spectrum
        image = image + sv[i] * ð‘(mesh)
    end
    #
    # Normalize the spectrum
    if count(x -> x > 0.0, sv) == npeak
        # All signs are positive. It is not for off-diagonal element.
        # We have to normalize the spectrum.
        image = image ./ trapz(mesh,image)
    end

    return SpectralFunction(mesh, image)
end

#=
*Remarks* :

For the correlation function ``G(\tau_i)``, the corresponding noisy
correlation function is given by

```math
G_{\rm noisy}(\tau_i) = G_{\rm exact}(\tau_i) +
    \frac{\sum_j e^{-|\tau_j-\tau_i|/\xi}R_j}
         {\sqrt{\sum_j e^{-2|\tau_j-\tau_i|/\xi}}},
```
where the sum is performed assuming periodic boundary conditions, ``\xi``
denotes the correlation length, and ``R_j \sim {\rm Normal}(0,\sigma)``.
Note that in the case that a normal distribution is used it is possible
for ``G_{\rm noisy}(\tau_i)`` to have a different sign to ``G(\tau_i)``.

See Phys. Rev. X 7, 041072 (2017) for more details.
=#

"""
    make_noise(rng::AbstractRNG, Ï„::Vector{F64}, Î´::F64, Î¾::F64)

Generate noise for an imaginary time correlation function ``G(\tau)``.
The noise is exponentially correlated in imaginary time. This function is
adopted from https://github.com/SmoQySuite/SmoQySynthAC.jl.

### Arguments
* rng -> Random number generator.
* Ï„ -> Vector specifying the imaginary time ``\tau`` grid.
* Î´ -> Standard deviation of the noise; controls the typical amplitude of the error.
* Î¾ -> Correlation length associated with the noise in imaginary time.

By default, the last element of ``\tau`` is assumed to be equal to the
inverse temperature, i.e., ``\tau[end] = \beta``.
"""
function make_noise(rng::AbstractRNG, Ï„::Vector{F64}, Î´::F64, Î¾::F64)
    # Evaluate length of imaginary time axis
    LÏ„ = length(Ï„) - 1

    # Initialize noise to zero
    noise = zeros(F64, LÏ„+1)

    # Evaluate normal distribution
    R = Î´ * randn(rng, LÏ„+1)

    # Get the inverse temperature
    Î² = Ï„[end]

    # Setup R and Ï„ arrays on interval Ï„ âˆˆ [0, Î²-Î”Ï„]
    Ï„â€² = @view Ï„[1:LÏ„]
    Râ€² = @view R[1:LÏ„]

    # Iterate over imaginary time: Outer
    @inbounds for i in eachindex(Râ€²)
        # Initialize normalization factor
        V = 0.0

        # Iterate over imaginary time: Inner
        for j in eachindex(Râ€²)
            # Calculate weight
            Î”Ï„ = abs(Ï„â€²[j] - Ï„â€²[i])
            Wáµ¢â±¼ = exp(-min(Î”Ï„, Î² - Î”Ï„) / Î¾)
            #
            # Update noise
            noise[i] += Râ€²[j] * Wáµ¢â±¼
            #
            # Update normalization
            V += (Wáµ¢â±¼) ^ 2
        end

        # Normalize noise
        noise[i] /= sqrt(V)
    end

    return noise
end

"""
    make_green(
        rng::AbstractRNG,
        sf::SpectralFunction,
        kernel::Matrix{F64},
        grid::AbstractGrid
    )

For given spectral function A and kernel matrix K, try to generate the
corresponding correlation function G (note that G â‰¡ KA).

### Arguments
* rng -> Random number generator.
* sf -> A SpectralFunction struct, A(Ï‰).
* kernel -> Kernel matrix.
* grid -> Grid for correlation function.

### Returns
* gf -> A GreenFunction struct.
"""
function make_green(
    rng::AbstractRNG,
    sf::SpectralFunction,
    kernel::Matrix{F64},
    grid::AbstractGrid
    )
    # Get the number of data bins per test
    # Now only imaginary time Green's function supports multiple data bins.
    nbins = get_t("nbins")
    if nbins > 1
        @assert get_t("grid") in ("ftime", "btime")
    end
    @printf("number of data bins : %6i\n", nbins)

    # Get the noise level
    # If Î´ < 0, it means noise-free.
    Î´ = get_t("noise")

    # Get correlation length of noise
    Î¾ = get_t("lcorr")

    # Get type of noise
    # Now only imaginary time Green's function supports correlated noise.
    tcorr = get_t("tcorr")
    if tcorr
        @assert get_t("grid") in ("ftime", "btime")
    end

    # Calculate Green's function
    green = reprod(sf.mesh, kernel, sf.image)

    # Setup standard deviation
    ngrid = length(green)
    if Î´ < 0.0
        Î´ = 0.0
        err = fill(1.0e-4, ngrid)
    else
        err = fill(Î´, ngrid)
    end

    # Setup random noise
    if tcorr
        if nbins == 1 # Single data bin
            noise = make_noise(rng, grid.Ï„, Î´, Î¾)
            return GreenFunction(grid, green .+ noise, err)
        else         # Multiple data bins
            GFArray = GreenFunction[]
            # For each data bin, the noise is different.
            for _ = 1:nbins
                noise = make_noise(rng, grid.Ï„, Î´, Î¾)
                push!(GFArray, GreenFunction(grid, green .+ noise, err))
            end
            @assert length(GFArray) == nbins
            return GFArray
        end
    else
        noise = randn(rng, F64, ngrid) * Î´
        return GreenFunction(grid, green .+ noise, err)
    end
end

"""
    make_grid(_grid = nothing)

To generate imaginary time grid or Masubara grid for many-body correlator.
It will return a sub-type of the AbstractGrid struct.

### Arguments
* _grid -> If it is present, we should use it to setup grid.

### Returns
* grid -> Imaginary time or imaginary frequency grid.

See also: [`AbstractGrid`](@ref).
"""
function make_grid(_grid = nothing)
    # Extract key parameters
    if isnothing(_grid)
        grid = get_t("grid")
    else
        grid = _grid
    end
    #
    ngrid = get_t("ngrid")
    Î² = get_t("beta")

    _grid = nothing
    @cswitch grid begin
        @case "ftime"
            _grid = FermionicImaginaryTimeGrid(ngrid, Î²)
            break

        @case "btime"
            _grid = BosonicImaginaryTimeGrid(ngrid, Î²)
            break

        @case "ffreq"
            _grid = FermionicMatsubaraGrid(ngrid, Î²)
            break

        @case "bfreq"
            _grid = BosonicMatsubaraGrid(ngrid, Î²)
            break

        @default
            sorry()
            break
    end

    return _grid
end

"""
    make_mesh(_ktype = nothing, _mesh = nothing)

Try to generate an uniform (linear) or non-uniform (non-linear) mesh for
the spectral function in real axis.

### Arguments
* _ktype -> If it is present, we should use it to setup ktype.
* _mesh -> If it is present, we should use it to setup mesh.

### Returns
* mesh -> Real frequency mesh. It should be a subtype of AbstractMesh.

See also: [`LinearMesh`](@ref), [`TangentMesh`](@ref), [`LorentzMesh`](@ref).
"""
function make_mesh(_ktype = nothing, _mesh = nothing)
    # Predefined parameters for mesh generation
    #
    # Note that the parameters `f1` and `cut` are only for the generation
    # of the non-uniform mesh.
    #
    f1::F64 = 2.1
    cut::F64 = 0.01

    # Setup parameters according to act.toml
    pmesh = get_t("pmesh")
    if !isa(pmesh, Missing)
        (length(pmesh) == 1) && begin
            Î“, = pmesh
            f1 = Î“
            cut = Î“
        end
    end

    # Get essential parameters
    if isnothing(_ktype)
        ktype = get_t("ktype")
    else
        ktype = _ktype
    end
    #
    if isnothing(_mesh)
        mesh = get_t("mesh")
    else
        mesh = _mesh
    end
    #
    nmesh = get_t("nmesh")
    wmax::F64 = get_t("wmax")
    wmin::F64 = get_t("wmin")
    #
    # For bosonic correlators of Hermitian operators, the spectral
    # function is defined in (0, âˆž) only.
    if ktype == "bsymm"
        @assert wmin â‰¥ 0.0
        @assert wmax â‰¥ 0.0
        @assert wmax > wmin
    end

    # Try to generate the required mesh
    @cswitch mesh begin
        @case "linear"
            return LinearMesh(nmesh, wmin, wmax)
            break

        @case "tangent"
            return TangentMesh(nmesh, wmin, wmax, f1)
            break

        @case "lorentz"
            return LorentzMesh(nmesh, wmin, wmax, cut)
            break

        @case "halflorentz"
            return HalfLorentzMesh(nmesh, wmax, cut)
            break

        @default
            sorry()
            break
    end
end

"""
    make_kernel(am::AbstractMesh, ag::AbstractGrid)

Try to generate various kernel functions.

### Arguments
* am -> Real frequency mesh.
* ag -> Imaginary axis grid.

### Returns
* kernel -> Kernel function, a 2D array, (ntime,nmesh) or (nfreq,nmesh).

See also: [`AbstractMesh`](@ref), [`AbstractGrid`](@ref).
"""
function make_kernel(am::AbstractMesh, ag::AbstractGrid)
    ktype = get_t("ktype")
    grid = get_t("grid")

    @cswitch ktype begin
        @case "fermi"
            @assert grid in ("ftime", "ffreq")
            return build_kernel(am, ag)
            break

        @case "boson"
            @assert grid in ("btime", "bfreq")
            return build_kernel(am, ag)
            break

        @case "bsymm"
            @assert grid in ("btime", "bfreq")
            return build_kernel_symm(am, ag)
            break

        @default
            sorry()
            break
    end
end
